plugins {
    id "java"
    id "eclipse"
    id "application" // gradle task plugin for distTar

    id "io.spring.dependency-management" version "1.1.7"
    id "org.springframework.boot" version "3.5.3"

    id "org.ajoberstar.grgit" version "4.1.1"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"  // add git information to actuator endpoint
    id "com.google.protobuf" version "0.9.5"
}

ext {
    ideJavaVersion='17'
    javaVersion='17'
    armeriaVersion='1.32.5'
    nettyVersion='4.2.1.Final'
    
    lettuceVersion='6.7.1.RELEASE'
    
    protocVersion='3.25.5'
    grpcVersion='1.71.0'

    hashVersion = "1.0.0-${grgit.head().abbreviatedId}"
}

logger.lifecycle("Current hash version: ${hashVersion}")
logger.lifecycle("Current branch: ${grgit.branch.current.name}")

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(ideJavaVersion))
  }
}

clean {
     delete fileTree("${projectDir}/gen-src") { include '**/*.java' }
}

tasks.withType(JavaCompile) {
    javaCompile -> logger.lifecycle("Hello, ${project.name} " + javaCompile.name)
    options.encoding = 'UTF-8'
    options.compilerArgs += '-parameters'
    // Do not use source and target compatibility with java.toolchain
    // sourceCompatibility = javaVersion
    // targetCompatibility = javaVersion
}

tasks.withType(ProcessResources) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.named('processResources') {
    // Logback 설정 파일만 제외 (Log4j2는 포함)
    exclude('application.properties', 'logback-spring.xml')
}

tasks.named('test') {
    useJUnitPlatform()
    // Gradle 실행 JVM의 -Dspring.profiles.active 값을 Test JVM으로 전달
    systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active", "")
}

eclipse {
    classpath {
        defaultOutputDir = file('classes')
        downloadJavadoc = false
        downloadSources = true

        file.whenMerged { classpath -> classpath.entries.sort() }
        
        // 캐시 클리어를 위한 설정
        file.withXml {
            def node = it.asNode()
            node.appendNode('classpathentry', [kind: 'con', path: 'org.eclipse.jdt.launching.JRE_CONTAINER'])
        }
    }
}

// 인코딩 관련 시스템 속성 설정
compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

group = 'com.github.armedis'
version = '1.0.0-SNAPSHOT'
description = """An HTTP2/gRPC interface for Redis, Support Redis stand-alone/cluster mode."""

application {
    mainClass = "com.github.armedis.ArmedisServer"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"

        // Set the location of generated source code by protoc compiler
        // generatedFilesBaseDir = "$projectDir/gen-src"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        proto {
          // In addition to the default 'src/main/proto'
          include '**/*.proto'
        }
        java {
            // add source folder order
          srcDirs 'src/main/java'
        }
        resources {
          srcDirs 'src/main/resources'
        }
    }
    test {
        java {
            srcDirs 'src/test/java'
        }
    }
}

repositories {
    mavenCentral()
}

configurations {
    all {
        // 기본 Logback starter 및 구 Log4j(1.x)·구 SLF4J 바인딩 차단
        exclude group: "org.springframework.boot", module : "spring-boot-starter-logging"
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "log4j", module: "log4j" // Log4j 1.x
        exclude group: "ch.qos.logback", module: "logback-classic" // 제3자 의존성 통해 유입 차단
    }
}

dependencyManagement {
    imports {
        mavenBom "com.linecorp.armeria:armeria-bom:${armeriaVersion}"
    }
}

dependencies {
    // gRPC
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    // Armeria
    ['armeria',
     'armeria-grpc',
     'armeria-spring-boot3-autoconfigure',
     'armeria-spring-boot3-actuator-starter',
     'armeria-zookeeper3'
    ].each {
        implementation "com.linecorp.armeria:${it}:${armeriaVersion}"
    }

    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-configuration-processor"
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    
    implementation 'redis.clients:jedis:5.1.0' // 서버 타입 감지용

    implementation 'org.apache.commons:commons-text:1.13.1'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'org.apache.commons:commons-pool2:2.12.1'
    implementation 'commons-io:commons-io:2.19.0'

    implementation "io.lettuce:lettuce-core:${lettuceVersion}"
    implementation "com.google.code.gson:gson:2.10.1"

    // === Logging: Log4j2만 사용 ===
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    runtimeOnly   "org.apache.logging.log4j:log4j-jul"   // (선택) JUL 로그를 Log4j2로 라우팅

    // 주석: slf4j-api, jcl-over-slf4j, logback-classic 는 제거 (starter-log4j2가 slf4j 바인딩을 제공)
    // compileOnly
    compileOnly "javax.annotation:javax.annotation-api:1.3.2"
    
    // Test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation platform("org.junit:junit-bom:5.12.2")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.junit.platform:junit-platform-suite"

    // json unit
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:5.1.0'
}